buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

tasks.register("updateGradleWrapper", Wrapper) {
    // https://docs.gradle.org/current/release-notes.html
    // https://github.com/gradle/gradle/releases
    gradleVersion = '7.0'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

//==================================================================================================

apply plugin: 'com.android.application'

android {
    compileSdkVersion 30

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.android.contacts"

        minSdkVersion 28
        targetSdkVersion 30

        versionCode 401
        versionName '4.0.1'

        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"

            debuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
        }
        debugProguard {
            applicationIdSuffix ".dev"

            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
        release {
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
    }

    dexOptions {
        dexInProcess true
        preDexLibraries true
    }

    lintOptions {
        baseline file("lint-baseline.xml")

        abortOnError true
        checkAllWarnings true
        showAll true

        warningsAsErrors true

        // HTML report is enough (can be found inside build/reports/)
        xmlReport false

        // do not warn about translations
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'

            aidl.srcDirs = ['src', 'src_ext']
            assets.srcDirs = ['assets']
            java.srcDirs = ['src', 'src_ext']
            renderscript.srcDirs = ['src', 'src_ext']
            res.srcDirs = ['res']
            resources.srcDirs = ['res']
        }
    }
}
configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}
dependencies {
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.guava:guava:24.1-jre'
    //noinspection GradleCompatible
    implementation "androidx.transition:transition:1.2.0"
    implementation "androidx.legacy:legacy-support-v13:1.0.0"
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    //noinspection GradleCompatible
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.googlecode.libphonenumber:libphonenumber:7.0'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation 'javax.inject:javax.inject:1'
    implementation 'com.googlecode.libphonenumber:geocoder:2.17'

}
